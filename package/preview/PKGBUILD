pkgname="carbonio-preview-ce"
pkgver="0.3.10"
pkgrel="SNAPSHOT"
pkgdesc="Carbonio Preview"
maintainer="Zextras <packages@zextras.com>"
copyright=(
  "2022-2024, Zextras <https://www.zextras.com>"
)
license=("AGPL-3.0-only")
url="https://github.com/zextras"
section="admin"
priority="optional"
arch=('x86_64')

depends__apt=(
  "pending-setups"
  "python3"
  "python3-setuptools"
  "service-discover"
)
depends__rocky_8=(
  "pending-setups"
  "python38"
  "python38-setuptools"
  "service-discover"
)
depends__rocky_9=(
  "pending-setups"
  "python3"
  "python3-setuptools"
  "service-discover"
)
makedepends__apt=(
  "git"
  "python3-pkg-resources"
  "python3-pip"
)
makedepends__rocky_8=(
  "git"
  "python38-pip"
)
makedepends__rocky_9=(
  "git"
  "python3-pip"
)

provides__rocky_8=(
  "libjpeg-cec335f2.so.62.4.0(LIBJPEG_6.2)(64bit)"
  "liblzma-d1e41b3a.so.5.4.5(XZ_5.0)(64bit)"
  "libpng16-36a62686.so.16.40.0(PNG16_0)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.3.4)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.9)(64bit)"
  "libtiff-f683b479.so.6.0.2(LIBTIFF_4.0)(64bit)"
  "/bin/python3.8"
)
provides__rocky_9=(
  "libjpeg-e44fd0cd.so.62.4.0(LIBJPEG_6.2)(64bit)"
  "liblzma-13fa198c.so.5.4.5(XZ_5.0)(64bit)"
  "libpng16-78d422d5.so.16.40.0(PNG16_0)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.3.4)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.9)(64bit)"
  "libtiff-91af027d.so.6.0.2(LIBTIFF_4.0)(64bit)"
  "/bin/python3"
)

source=(
  "carbonio-preview.service"
  "carbonio-preview-sidecar.service"
  "carbonio-preview-start.sh.in"
  "carbonio-preview.hcl"
  "carbonio-preview.sh"
  "carbonio-preview"
  "config.ini"
  "messages.ini"
  "intentions.json"
  "policies.json"
  "service-protocol.json"
)
sha256sums=('5c378a420cf3eb217fef72ce589d6dbee0ca6c0a054983a12287f92418e96ad5'
  '731e767bd1165f083420e23fa64f9e19ee518d823b685ecc4b78dada1b271792'
  '1716b914bf131f4a8f031b2491c1a0bc286fe8e4843be7317cd82c2ad90b43a2'
  '0d7b599c28ab35ba4d053fd36688b4bbea03390866ae28c4d0ad66d0c1fa1009'
  'f70942e22cffaa1b3783aece6275050cb70239573961c5c0cca30f83b5ba58ad'
  'e98ceeb36ca3d9d1eb5afc799cdbe412c0fce1b99083864132288ff5b28dcb9c'
  '941ad4db9950e5d8b24abcf8ba99ab03a59d87507102f8b428b84c7edd389110'
  'e193105f8e0710525b696ac9236f9c24e42f2b9fe1e8950331bc2026d9e698f2'
  '17aa6d19c18fcb58b11cd04ddc12e9aea89381453555f8095d8b875a27f5d639'
  '7c3dd74a3bab5260cea7b4876a8e472bc3258cc5bf35ebf3d02dae02d7c02ece'
  '1d2e022f32d45f5d5b1118fb524f4bafacc92689b76e7f24eea37b9fed6286fe')

backup=(
  "etc/zextras/service-discover/carbonio-preview.hcl"
  "etc/carbonio/preview/config.ini"
)

build__ubuntu_focal() {
  cd "${srcdir}"/../../staging

  pip3 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"

  # Copy the start script template to fakeroot
  install -Dm755 "${srcdir}/carbonio-preview-start.sh.in" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"

  # Replace python version and prefix according to distro
  # without modify the start script in srcdir.
  sed -i "s/PYTHON_VER/3.8/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
  sed -i "s/PREFIX/common/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
}

build__ubuntu_jammy() {
  cd "${srcdir}"/../../staging

  pip3 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"

  install -Dm755 "${srcdir}/carbonio-preview-start.sh.in" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"

  sed -i "s/PYTHON_VER/3.10/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
  sed -i "s/PREFIX/common\/local/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
}

build__rocky_8() {
  cd "${srcdir}"/../../staging

  # ugly workaround to disable rpm system-wide stripping
  # please don't execute outside of the build agent
  echo "#!/bin/sh" >/usr/lib/rpm/brp-python-bytecompile
  echo "#!/bin/sh" >/usr/lib/rpm/brp-strip-static-archive
  echo "#!/bin/sh" >/usr/lib/rpm/brp-strip

  pip3.8 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3.8 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"

  install -Dm755 "${srcdir}/carbonio-preview-start.sh.in" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"

  sed -i "s/PYTHON_VER/3.8/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
  sed -i "s/PREFIX/common/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
}

build__rocky_9() {
  cd "${srcdir}"/../../staging

  # ugly workaround to disable rpm system-wide stripping
  # please don't execute outside of the build agent
  echo "#!/bin/sh" >/usr/lib/rpm/brp-python-bytecompile
  echo "#!/bin/sh" >/usr/lib/rpm/brp-strip-static-archive
  echo "#!/bin/sh" >/usr/lib/rpm/brp-strip

  pip3 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"

  install -Dm755 "${srcdir}/carbonio-preview-start.sh.in" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"

  sed -i "s/PYTHON_VER/3.9/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
  sed -i "s/PREFIX/common/g" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"
}

package() {
  install -Dm755 "${srcdir}/carbonio-preview" \
    "${pkgdir}/usr/bin/carbonio-preview"

  install -Dm644 "${srcdir}/carbonio-preview.service" \
    "${pkgdir}/lib/systemd/system/carbonio-preview.service"

  install -Dm644 "${srcdir}/carbonio-preview-sidecar.service" \
    "${pkgdir}/lib/systemd/system/carbonio-preview-sidecar.service"

  install -Dm644 "${srcdir}/carbonio-preview.hcl" \
    "${pkgdir}/etc/zextras/service-discover/carbonio-preview.hcl"

  install -Dm755 "${srcdir}/carbonio-preview.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/carbonio-preview.sh"

  install -Dm644 "${srcdir}/intentions.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/intentions.json"

  install -Dm644 "${srcdir}/policies.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/policies.json"

  install -Dm644 "${srcdir}/service-protocol.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/service-protocol.json"

  install -Dm644 "${srcdir}/config.ini" \
    "${pkgdir}/etc/carbonio/preview/config.ini"

  install -Dm644 "${srcdir}/messages.ini" \
    "${pkgdir}/etc/carbonio/preview/messages.ini"

  install -Ddm755 "${pkgdir}/var/log/carbonio/preview/"

  # Remove generated bytecode
  find "${pkgdir}" -iname "*.pyc" -exec rm {} \;
}

postinst() {
  getent group 'carbonio-preview' >/dev/null ||
    groupadd -r 'carbonio-preview'
  getent passwd 'carbonio-preview' >/dev/null ||
    useradd -r -m \
      -d '/var/lib/carbonio-preview' \
      -g 'carbonio-preview' \
      -s /sbin/nologin 'carbonio-preview'

  chown carbonio-preview:carbonio-preview -R "/var/log/carbonio/preview"

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-preview.service >/dev/null 2>&1 || :
    systemctl enable carbonio-preview-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Preview installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-preview.service >/dev/null 2>&1 || :
    systemctl --no-reload disable carbonio-preview-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-preview.service >/dev/null 2>&1 || :
    systemctl stop carbonio-preview-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
