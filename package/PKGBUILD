targets=(
   "centos"
   "ubuntu"
)
pkgname="carbonio-preview-ce"
pkgver="0.3.3"
pkgrel="4"
pkgdesc="Carbonio Preview"
pkgdesclong=(
  "Carbonio Preview"
)
maintainer="Zextras <packages@zextras.com>"
url="https://www.zextras.com/"
section="admin"
priority="optional"
arch="amd64"
license=("AGPL-3.0-only")

depends:apt=(
   "pending-setups"
   "python3>=3.8"
   "service-discover"
)

depends:yum=(
  "carbonio-docs-core"
  "pending-setups"
  "python38"
  "service-discover"
)

makedepends:yum=(
  "python38-pip"
)

makedepends:apt=(
  "python3-pkg-resources"
  "python3-pip"
)

provides:yum=(
  "libjpeg-2caf4b68.so.62.3.0(LIBJPEG_6.2)(64bit)"
  "liblzma-72f7b2a5.so.5.4.2(XZ_5.0)(64bit)"
  "libpng16-021811b1.so.16.39.0(PNG16_0)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.3.4)(64bit)"
  "libz-dd453c56.so.1.2.11(ZLIB_1.2.9)(64bit)"
  "libtiff-ac0c3d92.so.6.0.0(LIBTIFF_4.0)(64bit)"
  "/bin/python3.8"
)

sources=(
  "carbonio-preview.service"
  "carbonio-preview-sidecar.service"
  "carbonio-preview-start.sh"
  "carbonio-preview.hcl"
  "carbonio-preview.sh"
  "carbonio-preview"
  "config.ini"
  "messages.ini"
  "intentions.json"
  "policies.json"
  "service-protocol.json"
)
hashsums=(
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
  "skip"
)

backup=(
  "/etc/zextras/service-discover/carbonio-preview.hcl"
  "/etc/carbonio/preview/config.ini"
)

build:apt() {
  cd "${srcdir}"/../staging

  pip3 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"
}

build:yum() {
  cd "${srcdir}"/../staging

  # ugly workaround to disable rpm system-wide stripping
  # please don't execute outside of the build agent
  echo "#!/bin/sh" > /usr/lib/rpm/brp-python-bytecompile
  echo "#!/bin/sh" > /usr/lib/rpm/brp-strip-static-archive
  echo "#!/bin/sh" > /usr/lib/rpm/brp-strip

  pip3.8 install \
    --prefix="${pkgdir}/opt/zextras/common" \
    -r requirements.txt

  python3.8 setup.py install \
    --no-compile \
    --prefix=/opt/zextras/common \
    --root="${pkgdir}"
}

package() {
  install -Dm755 "${srcdir}/carbonio-preview-start.sh" \
    "${pkgdir}/usr/bin/carbonio-preview-start.sh"

  install -Dm755 "${srcdir}/carbonio-preview" \
    "${pkgdir}/usr/bin/carbonio-preview"

  install -Dm644 "${srcdir}/carbonio-preview.service" \
    "${pkgdir}/lib/systemd/system/carbonio-preview.service"

  install -Dm644 "${srcdir}/carbonio-preview-sidecar.service" \
    "${pkgdir}/lib/systemd/system/carbonio-preview-sidecar.service"

  install -Dm644 "${srcdir}/carbonio-preview.hcl" \
    "${pkgdir}/etc/zextras/service-discover/carbonio-preview.hcl"

  install -Dm755 "${srcdir}/carbonio-preview.sh" \
    "${pkgdir}/etc/zextras/pending-setups.d/carbonio-preview.sh"

  install -Dm644 "${srcdir}/intentions.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/intentions.json"

  install -Dm644 "${srcdir}/policies.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/policies.json"

  install -Dm644 "${srcdir}/service-protocol.json" \
    "${pkgdir}/etc/carbonio/preview/service-discover/service-protocol.json"

  install -Dm644 "${srcdir}/config.ini" \
    "${pkgdir}/etc/carbonio/preview/config.ini"

  install -Dm644 "${srcdir}/messages.ini" \
    "${pkgdir}/etc/carbonio/preview/messages.ini"

  install -Ddm755 "${pkgdir}/var/log/carbonio/preview/"

  # Remove generated bytecode
  find "${pkgdir}" -iname "*.pyc" -exec rm {} \;
}

postinst() {
  getent group 'carbonio-preview' >/dev/null ||
    groupadd -r 'carbonio-preview'
  getent passwd 'carbonio-preview' >/dev/null ||
    useradd -r -m \
      -d '/var/lib/carbonio-preview' \
      -g 'carbonio-preview' \
      -s /sbin/nologin 'carbonio-preview'

  chown carbonio-preview:carbonio-preview -R "/var/log/carbonio/preview"

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-preview.service >/dev/null 2>&1 || :
    systemctl enable carbonio-preview-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "======================================================"
  echo "Carbonio Preview installed successfully!"
  echo "You must run pending-setups to configure it correctly."
  echo "======================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-preview.service >/dev/null 2>&1 || :
    systemctl --no-reload disable carbonio-preview-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-preview.service >/dev/null 2>&1 || :
    systemctl stop carbonio-preview-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}

